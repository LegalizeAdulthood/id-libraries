FROM:	INTERNET:fractint@lists.xmission.com, INTERNET:fractint@lists.xmission.com
TO:	(unknown), INTERNET:fractint@lists.xmission.com
DATE:	11/29/99 8:12 p

Re:	(fractint) 2D slices of Julibrot

Hello,

Here is a formula that produces 2D slices of the Julibrot set.  You can
get a feel for some of the types of images that the formula produces by
using the new evolver in v. 20.  After this formula, I give some hints on
how to use the formula.

SliceJB {; by John R. H. Goering, July 1999
;This formula produces 2D slices of the 4D Julibrot set. The numbers for
;p1 and p2 describe the rotation of the plane that slices the set (I
;call that plane the uv-plane -- the u-axis is the horizontal axis on 
;the screen and the v-axis is the vertical axis). I call the 4 axes
;in 4-space the p, q, r, & s axes. The M-set is in the pq-plane and the
;J-sets are in the rs-plane and planes "parallel" to it. The rotation
angles
;are to be entered as fractions of pi (e.g., pi/4 would be entered as
0.25).
;At first, the uv-plane is identical to the pq-plane.
;Then the rotations are performed counterclockwise in each plane as
follows:
;First: real(p1)--the rotation in the qs-plane.  Then imag(p1)--qr-plane.
;real(p2)--pr-plane. imag(p2)--pq-plane.
;The origin of the resulting skewed uv-plane is then placed at the point
;(real(p3), imag(p3), 0, 0) in pqrs-space. To create the M-set, leave the
;parameters alone, or you may change p3 to change the position of the
M-set 
;on the uv-plane.
;To create a J-set, set real(p1)=real(p2)=0.5, then set p3 equal to the
;constant for the J-set (let imag(p1)=imag(p2)=0).
;The parameters for p1 and p2 that are needed to put the uv-plane
parallel to
;the various coordinate planes are as following (the numbers are given in
the
;order -- real(p1), imag(p1), real(p2), imag(p2) ):
;pq-plane: 0, 0, 0, 0   pr-plane: 0, 0.5, 0, 0   ps-plane: 0.5, 0, 0, 0
;qr: 0, 0.5, 0, 0.5  qs: 0.5, 0, 0, 0.5  rs: 0.5, 0, 0.5, 0

  pix=pixel, u=real(pix), v=imag(pix), a=pi*real(p1), b=pi*imag(p1)
  g=pi*real(p2), d=pi*imag(p2), ca=cos(a), cb=cos(b), sb=sin(b),
cg=cos(g)
  sg=sin(g), cd=cos(d), sd=sin(d)
  p=u*cg*cd-v*(ca*sb*sg*cd+ca*cb*sd)
  q=u*cg*sd+v*(ca*cb*cd-ca*sb*sg*sd)
  r=u*sg+v*ca*sb*cg, s=v*sin(a), c=p+flip(q) + p3, z=r+flip(s):
  z=z*z+c
  |z|<=4
}

Example 1:  Set real(p1)=real(p2)=0.2; real(p3)=-0.1; and imag(p3)=0.7
(with the other parameters 0)
Then zoom in on some of the detail.  Notice that there are places that
look like the M-set and a J-set have been "mixed" together to create the
detail.

Example 2: This example demonstrates one way to create a sequence of
images that morph from the M-set to a particular J-set:
First set real(p1)=real(p2)=0.5; real(p3)=-0.52; and imag(p3)=0.57 (with
the other parameters 0)
This should generate a J-set.
Then press ctrl-e to enter evolver mode.  Change evolution mode to "yes"
and then press F6.  Change Param 1 real to "x", Param 2 real to "y". 
Make sure all the other settings are "no".  Then press enter.
You should now be able to change x parameter range to 0.5, x parameter
offset to 0, y parameter range to 0.5, and y parameter offset to 0.  Then
press enter.
You should now see a screen full of images.  The lower left image is a
perfect M-set and the upper right image is a perfect J-set. 
(On my computer, I also see the same J-set in the center [is this a
bug?].  The J-set does not belong there.  To put the correct image in the
center, first choose the image that is to the left of the center and
press enter.  On the resulting screen, choose the image that is to the
right of the center and press enter.  The correct image should now be in
the center with the M and J sets at the lower left and upper right
corners.)
You can now pick any path from the lower left of the screen to the upper
right of the screen to get a sequence of images that morph from the M-set
to the J-set.

Example 3: Set real(p1)=real(p2)=0.25 (with the other parameters set to
0)
This appears to create an M-set that has been magnified by a factor of
the sqrt(2).  Apparently, slicing the Julibrot at this particular angle
exposes a magnified M-set.  Does anyone know why this happens?

Example 4: Set imag(p1)=0.5 and all other parameters to 0.
You will need to zoom out to get the "big picture."
I think that this looks like a space probe entering the atmosphere.

Example 5: Use the evolver to experiment!

Some notes:
1) All angle measurements are in radians.

2) I sure wish that Fractint had 8 parameters for formulas instead of
just 6. With 2 more parameters, I could place the origin of the uv-plane
anywhere in pqrs-space instead of just somewhere on the pq-plane.

3) If anyone would like to see the mathematical derivation of this
formula, I would be willing to post it.

Yours truly,
John Ralph


-------------------------------------
John Ralph H. Goering
johnrhg@juno.com
-------------------------------------
 
At 08:02 PM 11/29/99 -0500, John Goering wrote:

              <snipped>

>This appears to create an M-set that has been magnified by a factor of
>the sqrt(2).  Apparently, slicing the Julibrot at this particular angle
>exposes a magnified M-set.  Does anyone know why this happens?

It happens because there actually are two slices that cut the 4-D 
Julibrot in perfect Mandelbrot sets.  The multirot-XY-ZW formula below 
draws this oversized M-set if p1 is set to 45,45  p2 and p3 are set 
to 0, and the functions are set to ident,flip.  I call this second 
M-set the shadow set.  It is double-rotated halfway between the 
Mandelbrot and Julia planes.

>2) I sure wish that Fractint had 8 parameters for formulas instead of
>just 6.

Me too!  10 would be even better.  Developers, hint, hint.

>3) If anyone would like to see the mathematical derivation of this
>formula, I would be willing to post it.

Yes, post the math derivation.  I'm still trying to find a single 
formula that with only 6 variable parameters can draw all the oblique 
slices through any point of the the Julibrot.

The two formulas below can fix any point in the 4-D Julibrot on the 
screen and rotate the slice around this point.  But I had to give up 
some rotations to achieve this.  The rotations, which are set by p1, 
are in degrees.  The second formula works only with the two functions 
set to ident,flip or flip,ident.  You will soon recognize the slices.  
I have given the four new slices the names Parabolic, Oblate, Elliptic 
and Rectangular to go with the well-known Julia and Mandelbrot slices.

============================================================

multirot-XZ-YW {; Jim Muth
; p1= 0,0=para, 90,0=obl, 0,90=elip, 90,90=rect
e=exp(flip(real(p1*.01745329251994))),
f=exp(flip(imag(p1*.01745329251994))),
z=f*real(pixel)+p2, c=e*imag(pixel)+p3:
z=sqr(z)+c,
|z| <= 36  }

multirot-XY-ZW {; draws 6 planes and many rotations
;when fn1-2=i,f, then p1 0,0=M, 0,90=O, 90,0=E, 90,90=J
;when fn1-2=f,i, then p1 0,0=M, 0,90=R, 90,0=P, 90,90=J
a=real(p1)*.01745329251994, b=imag(p1)*.01745329251994,
z=sin(b)*fn1(real(pixel))+sin(a)*fn2(imag(pixel))+p2,
c=cos(b)*real(pixel)+cos(a)*flip(imag(pixel))+p3:
z=sqr(z)+c,
|z| <= 36  }

===========================================================

Jim Muth
jamth@mindspring.com

{FROM:	INTERNET:fractint@lists.xmission.com, INTERNET:fractint@lists.xmission.com
TO:	(unknown), INTERNET:fractint@lists.xmission.com
DATE:	12/3/99 8:01 p

Re:	Re: (fractint) Re: 2D slices of Julibrot
 
Hello to all,

Here is the mathematical derivation of the formula that I posted:

First, I created the following parameterization of a plane in pqrs-space:
  p=u,  q=v,  r=0, and s=0 (where the u-axis is the horizontal axis on
the screen and the v-axis is the vertical axis).

Then I did a sequence of counterclockwise rotations in the following
planes: qs-plane, qr-plane, pr-plane, pq-plane.  I performed these
rotations using the following general formula for performing a rotation
in some xy-plane by some angle t:
 
xnew=xold*cos(t) - yold*sin(t)
ynew=xold*sin(t) + yold*cos(t)

First, the rotation in the qs-plane (where a is the angle of rotation):

q=v*cos(a) - 0*sin(a)
s=v*sin(a) + 0*cos(a)

(Note that qold=v and sold=0 according to the first parameterization. 
This rotation creates a new parameterization for q and s.)

So now the complete parameterization of the uv-plane in pqrs-space is:

p=u
q=v*cos(a)
r=0
s=v*sin(a)

Then the rotation in the qr-plane (where b is the angle of rotation and
where I substitute the "old" values for q and r from the above
parameterization into the rotation equations):

q=[v*cos(a)]*cos(b) - 0*sin(b)
r=[v*cos(a)]*sin(b) + 0*cos(b)

The new parameterization in pqrs-space then is:

p=u
q=v*cos(a)*cos(b)
r=v*cos(a)*sin(b)
s=v*sin(a)

Then the rotation in the pr-plane (where g is the angle of rotation [if
you wonder where this sequence of letters is from, I originally use the
first for letters of the Greek alphabet as the angles of rotation]):

p=u*cos(g) - [v*cos(a)*sin(b)]*sin(g)
r=u*sin(g) + [v*cos(a)*sin(b)]*cos(g)

So the new parameterization in pqrs-space is:

p=u*cos(g) - v*cos(a)*sin(b)*sin(g)
q=v*cos(a)*cos(b)
r=u*sin(g) + v*cos(a)*sin(b)*cos(g)
s=v*sin(a)

Then the rotation in the pq-plane (where d is the angle of rotation):

p=[u*cos(g) - v*cos(a)*sin(b)*sin(g)]*cos(d) - [v*cos(a)*cos(b)]*sin(d)
q=[u*cos(g) - v*cos(a)*sin(b)*sin(g)]*sin(d) + [v*cos(a)*cos(b)]*cos(d)

After some mathematical manipulations, the complete parameterization is:

p=u*cos(g)*cos(d) - v*[cos(a)*sin(b)*sin(g)*cos(d) +
cos(a)*cos(b)*sin(d)]
q=u*cos(g)*sin(d) + v*[cos(a)*cos(b)*cos(d) -
cos(a)*sin(b)*sin(g)*sin(d)]
r=u*sin(g) + v*cos(a)*sin(b)*cos(g)
s=v*sin(a)

Then I performed a simple shift of the origin of the uv-plane to the
point (real(p3), imag(p3), 0 ,0).  This point is in the pq-plane ( the
plane that the M-set is in).  So, theoretically, the resulting
parameterizations of p and q are:

p=u*cos(g)*cos(d) - v*[cos(a)*sin(b)*sin(g)*cos(d) +
cos(a)*cos(b)*sin(d)] + real(p3)
q=u*cos(g)*sin(d) + v*[cos(a)*cos(b)*cos(d) -
cos(a)*sin(b)*sin(g)*sin(d)] + imag(p3)

However, to simplify things, I did not really add real(p3) and imag(p3)
in the formula until I initialized the value of c

So in implementing the parameterization, I initially set z = r + s*i and
c = p + q*i + p3 (where p3 is the third parameter. Note that this adds
the real portion of p3 to p and the imaginary portion of p3 to q.)

I included the formula below so that you can compare the parameterization
with how I wrote it in the actual code.
}

SliceJB {; by John R. H. Goering, July 1999
;This formula produces 2D slices of the 4D Julibrot set. The numbers for
;p1 and p2 describe the rotation of the plane that slices the set (I
;call that plane the uv-plane -- the u-axis is the horizontal axis on 
;the screen and the v-axis is the vertical axis). I call the 4 axes
;in 4-space the p, q, r, & s axes. The M-set is in the pq-plane and the
;J-sets are in the rs-plane and planes "parallel" to it. The rotation
angles
;are to be entered as fractions of pi (e.g., pi/4 would be entered as
0.25).
;At first, the uv-plane is identical to the pq-plane.
;Then the rotations are performed counterclockwise in each plane as
follows:
;First: real(p1)--the rotation in the qs-plane.  Then imag(p1)--qr-plane.
;real(p2)--pr-plane. imag(p2)--pq-plane.
;The origin of the resulting skewed uv-plane is then placed at the point
;(real(p3), imag(p3), 0, 0) in pqrs-space. To create the M-set, leave the
;parameters alone, or you may change p3 to change the position of the
M-set 
;on the uv-plane.
;To create a J-set, set real(p1)=real(p2)=0.5, then set p3 equal to the
;constant for the J-set (let imag(p1)=imag(p2)=0).
;The parameters for p1 and p2 that are needed to put the uv-plane
parallel to
;the various coordinate planes are as following (the numbers are given in
the
;order -- real(p1), imag(p1), real(p2), imag(p2) ):
;pq-plane: 0, 0, 0, 0   pr-plane: 0, 0.5, 0, 0   ps-plane: 0.5, 0, 0, 0
;qr: 0, 0.5, 0, 0.5  qs: 0.5, 0, 0, 0.5  rs: 0.5, 0, 0.5, 0

  pix=pixel, u=real(pix), v=imag(pix), a=pi*real(p1), b=pi*imag(p1)
  g=pi*real(p2), d=pi*imag(p2), ca=cos(a), cb=cos(b), sb=sin(b), cg=cos(g)
  sg=sin(g), cd=cos(d), sd=sin(d)
  p=u*cg*cd-v*(ca*sb*sg*cd+ca*cb*sd)
  q=u*cg*sd+v*(ca*cb*cd-ca*sb*sg*sd)
  r=u*sg+v*ca*sb*cg, s=v*sin(a), c=p+flip(q) + p3, z=r+flip(s):
  z=z*z+c
  |z|<=4
}

-------------------------------------
John Ralph H. Goering
johnrhg@juno.com
-------------------------------------
