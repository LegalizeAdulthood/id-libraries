
frm:Newt3_Atan_Mset {; Paul Carlson
    ; F(w)   = w^3 + (c - 1) * w - c
    ; F'(w)  = 3 * w^2 + c - 1
    ; F''(w) = 6 * w,  so initialize w to zero
    ;
    ; p1 contols the "layering"
    ;
    c = pixel , c1 = c-1
    z = iter = bailout = prev_w = w = 0
    colors_in_range = 80
    colors_in_range_1 = 79
    k = 0.5 * sqrt(1 - 4 * c)
    root1 = (1,0)
    root2 = -0.5 + k
    root3 = -0.5 - k
    :
    ;
    w2 = w*w
    w = w - (w*(w2 + c1) - c) / (3*w2 + c1)
    ;
    delta_w = w - prev_w
    IF (|delta_w| < p1)
      angle = atan(imag(delta_w)/real(delta_w))
      IF (delta_w >= 0)
        IF (imag(delta_w) < 0)
          angle = pi + angle
        ELSE
          angle = pi - angle
        ENDIF
      ELSEIF (imag(delta_w) > 0)
        angle = -angle
      ENDIF
      IF     (|w-root1| < p1)
        range_num = 0
      ELSEIF (|w-root2| < p1)
        range_num = 1
      ELSEIF (|w-root3| < p1)
        range_num = 2
      ENDIF
      bailout = 1
      z = colors_in_range_1 * angle / pi + range_num * colors_in_range + 1
    ENDIF
    prev_w = w
    iter = iter + 1
    z = z - iter
    bailout == 0
}

frm:Newt3_Atan_orig {; Paul Carlson
    ; F(w)   = w^3 + (c - 1) * w - c
    ; F'(w)  = 3 * w^2 + c - 1
    ; F''(w) = 6 * w,  so initialize w to zero
    ;
    ; p1 contols the "layering"
    ;
    c = pixel
    z = 0
    iter = 0
    bailout = 0
    colors_in_range = 80
    k = 0.5 * sqrt(1 - 4 * c)
    root1 = (1,0),    rr1 = real(root1), ri1 = imag(root1)
    root2 = -0.5 + k, rr2 = real(root2), ri2 = imag(root2)
    root3 = -0.5 - k, rr3 = real(root3), ri3 = imag(root3)
    prev_w = w = 0:
    ;
    w = w - (w*w*w + (c-1)*w - c) / (3*w*w + c - 1)
    ;
    wr = real(w), wi = imag(w)
    pr = real(prev_w), pm = imag(prev_w)
    prev_w = w
    IF (((wr-pr)*(wr-pr) + (wi-pm)*(wi-pm)) < p1)
   angle = atan((wi-pm)/(wr-pr))
   IF ((wr >= pr) && (wi < pm))
       angle = pi + angle
   ELSEIF ((wr >= pr) && (wi >= pm))
       angle = pi - angle
   ELSEIF ((wr < pr) && (wi > pm))
       angle = -angle
   ENDIF
   IF     (((wr-rr1)*(wr-rr1) + (wi-ri1)*(wi-ri1)) < p1)
       range_num = 0
   ELSEIF (((wr-rr2)*(wr-rr2) + (wi-ri2)*(wi-ri2)) < p1)
       range_num = 1
   ELSEIF (((wr-rr3)*(wr-rr3) + (wi-ri3)*(wi-ri3)) < p1)
       range_num = 2
   ENDIF
   bailout = 1
   index = (colors_in_range - 1) * angle / pi
    ENDIF
    IF (bailout)
   z = index + range_num * colors_in_range + 1
    ENDIF
    iter = iter + 1
    z = z - iter
    bailout == 0
}

n3atan06           {
  reset=1960 type=formula formulafile=n3atnm.frm
  formulaname=Newt3_Atan_Mset passes=t
  center-mag=+0.31671548334072320/+0.00018531398540474/60613\
  3.4/1/-157.5
  params=0.05/0 float=y maxiter=2000 inside=253 outside=summ
  colors=000z88<78>O000CC<78>0zz00O<78>GGz000<13>000
  }

colormap-1         { ; Sylvie Gallet, Mar 04, 1998
  colors=442zzn<29>voTunSunStmS<45>VNAxxn<30>imbimbhlagla<44>DNDxxn<30>imb\
  imbhlagla<44>DND000<13>000
  }

colormap-2         { ; Sylvie Gallet, Mar 04, 1998
  colors=038rrz<78>HJSvvy<78>QHIQHIQIJ<77>vvy000<13>000
  }

colormap-3         { ; Sylvie Gallet, Mar 04, 1998
  colors=zzzzll<29>lKUkJTkJTjJS<45>A05www<78>ADIADIBEJ<77>www000987<10>111\
  zmmzmm
  }

colormap-4         { ; Sylvie Gallet, Mar 04, 1998
  colors=855wwp<27>piPpiOohMngL<13>fU7eT6eS5eS5<29>O31N20O41N20<10>Q72R82R\
  82S93TA3<14>gQ5hR5iS4<3>mW3nX2nX2nY2<6>rc0rc0rb0<4>pZ1pY1oX2nX2<11>aL4`K\
  4ZI3<7>P50CA6<78>tts000<10>J33P84P73O52
  }

colormap-5         { ; Sylvie Gallet, Mar 04, 1998
  colors=000zzz<78>FFFwzw<21>bhdbhdagc`gc`fb<10>T_WSZVSZVSZV<12>PVQOUPOUPN\
  TONTN<10>HNGHMFGLEFKDFJC<6>AF5EF0<30>`XJaYKaYKaYL<28>nj_nj_ok`ola<12>xuk\
  669000<12>000
  }

